managed implementation in class ZBP_RAP_TRAVEL_00 unique;
strict ( 2 );
with draft;

define behavior for ZR_RAP_TRAVEL_00 alias Travel
persistent table zrap_travel_00
draft table zrap_travel_00_d
etag master LocalLastChangedAt
lock master total etag LastChangedAt
early numbering
authorization master ( global )

{
  field ( mandatory )
  CustomerID,
  BeginDate,
  EndDate;

  field ( readonly )
  CreatedAt,
  CreatedBy,
  LastChangedAt,
  LocalLastChangedAt,
  LocalLastChangedBy,

  //  field ( readonly : update )
  TravelID;


  create;
  update;
  delete;

  determination setStatusToOpen on modify { create; }

  validation validateCustomer on save { create; field CustomerID; }
  validation validateDates on save { create; field BeginDate, EndDate; }

  draft action Edit;
  draft action Activate optimized;
  draft action Discard;
  draft action Resume;
  draft determine action Prepare
  {
    validation validateCustomer;
    validation validateDates; }


  mapping for zrap_travel_00
    {
      TravelID           = travel_id;
      AgencyID           = agency_id;
      CustomerID         = customer_id;
      BeginDate          = begin_date;
      EndDate            = end_date;
      BookingFee         = booking_fee;
      TotalPrice         = total_price;
      CurrencyCode       = currency_code;
      Description        = description;
      OverallStatus      = overall_status;
      Attachment         = attachment;
      MimeType           = mime_type;
      FileName           = file_name;
      CreatedBy          = created_by;
      CreatedAt          = created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}